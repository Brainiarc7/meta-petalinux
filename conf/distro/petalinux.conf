require conf/distro/poky.conf

DISTRO = "petalinux"
DISTRO_NAME = "PetaLinux"
DISTRO_VERSION = "2016.3"
DISTRO_CODENAME = "krogoth"
SDK_VENDOR = "-petalinux"
SDK_VERSION := "${@'${DISTRO_VERSION}'.replace('snapshot-${DATE}','snapshot')}"

MAINTAINER = "Xilinx <meta-xilinx@yoctoproject.org>"

TARGET_VENDOR = "-xilinx"

IMAGE_FSTYPES ?= "ext3.bz2 tar.bz2 cpio.bz2"

POKY_DEFAULT_DISTRO_FEATURES = "largefile ptest multiarch"

# BusyBox features
BUSYBOX_FEATURES = " \
	busybox-hd \
	busybox-ftpd \
	busybox-ftp \
	busybox-telnetd \
	busybox-mdev \
	busybox-inetd \
	busybox-httpd \
	"

POKY_DEFAULT_DISTRO_FEATURES_append_microblaze = " ${BUSYBOX_FEATURES}"
POKY_DEFAULT_DISTRO_FEATURES_append_zynq = " ${BUSYBOX_FEATURES} x11 glx opengl"
POKY_DEFAULT_DISTRO_FEATURES_append_zynqmp = " ${BUSYBOX_FEATURES} x11 glx opengl xen"

DISTRO_FEATURES_remove_microblaze = "bluetooth wifi 3g nfc"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-xlnx"
PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-xlnx"

PREFERRED_PROVIDER_jpeg ?= "jpeg"
PREFERRED_PROVIDER_jpeg-native ?= "jpeg-native"
PREFERRED_PROVIDER_opencv-samples ?= "opencv-samples"
PREFERRED_PROVIDER_tcf-agent_microblaze = "tcf-agent-microblaze"

VIRTUAL-RUNTIME_dev_manager ?= "busybox-mdev"
PREFERRED_PROVIDER_virtual/gettext ?= "gettext"
PREFERRED_VERSION_qwt ?= "6.1.2"
PREFERRED_VERSION_qemu ?= "xilinx"
PREFERRED_PROVIDER_iasl-native ?= "iasl-native"

PREFERRED_VERSION_xen = "4.7%"

# Blacklist packages
INHERIT += "blacklist"
PNBLACKLIST[distro-feed-configs]  = "Not supported, see petalinux-image-common.inc for feed config."

PNBLACKLIST[lttng-modules] = "Not supported, built as machine dependent."
PNBLACKLIST[perf]          = "Not supported, built as machine dependent."
RDEPENDS_packagegroup-core-tools-profile_remove    = "lttng-modules"
RRECOMMENDS_packagegroup-core-tools-profile_remove = "perf"

LICENSE_FLAGS_WHITELIST = "commercial_ffmpeg commercial_x264"

WARN_TO_ERROR_QA_remove = "installed-vs-shipped"
